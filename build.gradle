buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'co.tomlee.gradle.plugins:gradle-thrift-plugin:+'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

group 'ru.aatarasoff.thrift'
version '0.2.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'thrift'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'org.apache.thrift:libthrift:0.9.2'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.apache.commons:commons-lang3:3.4'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

generateTestThriftSource {
    out file('generated-src/thrift')

    verbose false
    debug false
    strict false

    generators {
        java {
            option 'hashcode'
            option 'private-members'
        }
    }
}

clean {
    delete "generated-src/thrift"
}

sourceSets {
    test {
        java {
            srcDir 'generated-src/thrift'
        }
    }
}

idea.module.excludeDirs = []
idea.module.testSourceDirs += file('generated-src/thrift')
idea.module.generatedSourceDirs += file('generated-src/thrift')

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = System.properties['BINTRAY_USER']
    key = System.properties['BINTRAY_KEY']
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = 'thrift-api-gateway-core'
    }
}